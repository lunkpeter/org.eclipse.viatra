/*******************************************************************************
 * Copyright (c) 2004-2015, Peter Lunk, Zoltan Ujhelyi and Daniel Varro
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   Peter Lunk - initial API and implementation
 *******************************************************************************/
module org.eclipse.viatra.emf.mwe2integration.example.ParallelExample

//Import the necessary libraries
//Note, that the package containing the workflow needs to be imported as well (if it contains used resources)
import org.eclipse.viatra.emf.mwe2integration.*
import org.eclipse.viatra.emf.mwe2integration.mwe2impl.*
import org.eclipse.viatra.emf.mwe2integration.mwe2impl.messages.*
import org.eclipse.viatra.emf.mwe2integration.examples.resources.*

//The workflow and components are defined in the usual MWE2 fashion
Workflow {
	//Add a TransformationChain component
	//This component represents an MWE2 component that can contain numerous transformations
	component = TransformationChain {
		//It is possible to define sections that are executed parallel
		//In this case, each contained transformation step is executed in separate threads.
		step = Parallel{
			//In this parallel section the for loop containing 3 A transformation steps and
			//A B transformation step is executed parallel
			step = ForLoop{
				iterations = "3"
				step = ExampleATransformationStep{}
			}
			
			step = ExampleBTransformationStep{}
		}
		
		//This Additional step will be executed AFTER all of the parallel sections are executed
		//This is similar to the FORK-JOIN semantics of UML activity diagrams
		step = ExampleBTransformationStep{}
		
		//In this case, As far as the execution sequence is concerned, no exact sequence can be defined 
		//due to the parallel nature of this workflow
		//The only fact that can be specified is that the second B transformation step will be executed
		//AFTER the parallel section
	}
	//You can add additional regular MWE2 components here
}